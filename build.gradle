buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.3"
    id "groovy"
    id "com.gradle.plugin-publish" version "0.10.0"
    id "java-gradle-plugin"
}



repositories {
    mavenCentral()
    jcenter()
}

group = 'com.github.grimmjo'
version = '0.2'

dependencies {
    compile gradleApi()
    compile 'com.sebastian-daschner:jaxrs-analyzer:0.17'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "grimmjo"
            name "Joachim Grimm"
            email "grimm.jo@web.de"
        }
    }

    scm {
        url "https://github.com/grimmjo/jaxrs-analyzer-gradle-plugin"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'JAX-RS Analyzer Gradle Plugin')
                root.appendNode('name', 'JAX-RS Analyzer Gradle Plugin')
                root.appendNode('url', 'https://github.com/grimmjo/jaxrs-analyzer-gradle-plugin')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = 'maven-releases'
        name = 'jaxrs-analyzer-gradle-plugin'
        userOrg = 'grimmjo'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/grimmjo/jaxrs-analyzer-gradle-plugin.git'
        version {
            name = project.version
            desc = project.version
            released  = new Date()
        }
    }
}

gradlePlugin {
    plugins {
        analyzerPlugin {
            id = 'com.github.grimmjo'
            implementationClass = 'com.github.grimmjo.jaxrs_analyzer.gradle.JaxRsAnalyzerPlugin'
        }
    }
}


pluginBundle {
    website = 'https://github.com/grimmjo/jaxrs-analyzer-gradle-plugin'
    vcsUrl = 'https://github.com/grimmjo/jaxrs-analyzer-gradle-plugin'
    description = 'Gradle plugin for the JAX-RS Analyzer.'
    plugins {

        // first plugin
        analyzerPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'JAX-RS Analyzer Gradle Plugin'
            tags = ['jaxrs', 'swagger', 'asciidoc']
            version = project.version
        }
    }

    mavenCoordinates {
        groupId = "com.github.grimmjo"
        artifactId = "jaxrs-analyzer-gradle-plugin"
        version = project.version
    }
}
